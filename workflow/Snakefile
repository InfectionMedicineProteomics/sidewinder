#!/usr/bin/env python3
"""Based on the TX-MS web-app implementation Cheetah-MS.
"""

__author__ = 'Joel Str√∂baek'
__email__ = 'joel.strobaek@gmail.com'


# TODO:
# - Remove ms2 parameter hard coding!
#       - Set optional click input params with defaults


from pathlib import Path

from snakemake.utils import min_version


min_version("6.0")  # Min version for using modules.

configfile: "config/config.yml"


# Convert config paths from string to Path objects.
config_paths = ['output_dir', 'input_pdb1', 'input_pdb2', 'input_ms2']
config = {k: (v if k not in config_paths else Path(v).resolve()) for k, v in config.items()}

OUTDIR = config['output_dir']

MSCONVERT_CMD = (f'cp {{input.ms2}} {{output.mgf}}'
                 if (config['input_ms2'].suffix.lower() in '.mgf')
                 else f'msconvert {{input.ms2}} --mgf -o {{params.outdir}}')


rule all:
    # TODO: add in proper wildcards!
    #
    input:
        dck = OUTDIR / 'model_A-model_B.out',
        mgf = OUTDIR / 'ms2_convert' / f'{config["input_ms2"].stem}.mgf',
        sql = OUTDIR / 'ms2_results.sql',
        img_dir = OUTDIR / 'top_spectra',
        top_xls = OUTDIR / 'top_xls.txt',
        top_model = OUTDIR / 'best_model.pdb',
        top_model_xls = OUTDIR / 'best_model_xl.txt'

rule pdb_handling:
    input:
        PDB_a = config['input_pdb1'],
        PDB_b = config['input_pdb2']
    output:
        PDB = OUTDIR / 'complex_AB.pdb',
        seq_a = OUTDIR / 'seq_A.txt',
        seq_b = OUTDIR / 'seq_B.txt',
        mod_a = OUTDIR / 'model_A.pdb',
        mod_b = OUTDIR / 'model_B.pdb'
    params:
        pdb_handling = 'workflow/scripts/utils/pdb_handling.py'
    conda:
        'envs/biopython_env.yml'
    shell:
        "python3 {params.pdb_handling} "
        "--pdb1 {input.PDB_a} "
        "--pdb2 {input.PDB_b} "
        "--output_dir {OUTDIR}"

rule seq2xl:
    input:
        seq_a = rules.pdb_handling.output.seq_a,
        seq_b = rules.pdb_handling.output.seq_b
    output:
        OUTDIR / 'all_xls.txt'
    params:
        seq2xl = 'workflow/scripts/utils/cheetah/seq2xl.py'
    conda:
        'envs/biopython_env.yml'
    shell:
        "python3 {params.seq2xl} "
        "--seq_file1 {input.seq_a} "
        "--seq_file2 {input.seq_b} "
        "--output_dir {OUTDIR}"

rule megadock:
    # TODO: re-write to use native docker support instead of the python SDK...
    #
    input:
        mod_a = rules.pdb_handling.output.mod_a,
        mod_b = rules.pdb_handling.output.mod_b
    output:
        rules.all.input.dck
    params:
        megadock = 'workflow/scripts/utils/cheetah/megadock.py'
    conda:
        'envs/biopython_env.yml'
    shell:
        "python3  {params.megadock} "
        "--receptor {input.mod_a} "
        "--ligand {input.mod_b} "
        "--output_dir {OUTDIR} "
        "--n_out 1000"

rule msconvert:
    input:
        ms2 = config['input_ms2']
    output:
        mgf = rules.all.input.mgf
    params:
        outdir = OUTDIR / 'ms2_convert',
    conda:
        'envs/proteowizard_env.yml'
    shell:
        MSCONVERT_CMD  # Adjust shell command based on file suffix:
                       #    - if not '.mgf': convert
                       #    - if '.mgf': copy existing file to outdir

rule ms2:
    input:
        mgf = rules.msconvert.output.mgf,
        xls = rules.seq2xl.output
    output:
        sql = rules.all.input.sql,
        img_dir = directory(rules.all.input.img_dir),
        top_xls = rules.all.input.top_xls
    params:
        ms2_script = 'workflow/scripts/utils/cheetah/ms2.py'
    conda:
        'envs/pyteomics_env.yml'
    shell:
        "python3 {params.ms2_script} "
        "--mgf_file {input.mgf} "
        "--xl_file {input.xls} "
        "--output_dir {OUTDIR}"

rule modeling:
    input:
        PDB = rules.pdb_handling.output.PDB,
        top_xls = rules.ms2.output.top_xls
    output:
        top_model = rules.all.input.top_model,
        top_model_xls = rules.all.input.top_model_xls
    params:
        cut_off = config['distance_cut_off'],
        n_top_f = config['num_of_top_filters'],
        modeling_script = 'workflow/scripts/hamed_model.py'
    conda:
        'envs/biopython_env.yml'
    shell:
        "python3 {params.modeling_script} "
        "--complex_pdb {input.PDB} "
        "--output_dir {OUTDIR} "
        "--top_xls {input.top_xls}"

# rule visualization:
